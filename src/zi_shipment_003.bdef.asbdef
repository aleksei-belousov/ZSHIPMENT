managed with additional save implementation in class zbp_i_shipment_003 unique;
strict ( 2 );
with draft;
define behavior for ZI_SHIPMENT_003 alias Shipment
persistent table zshipment003
draft table zshipment003d
lock master
total etag LastChangedAt
authorization master ( instance )
etag master LocalLastChangedAt
{

  create;
  update;
  delete;

  draft determine action Prepare;
  draft action Resume with additional implementation;
  draft action Edit with additional implementation;
  draft action Activate with additional implementation optimized;
  draft action Discard /*with additional implementation*/;

  // Actions:
  action release;
  action create_invoice;
  action create_tkz_list;
  action create_tariff_document;
  action create_eci; // csv

  // Events:
  determination on_create on modify { field ShipmentUUID; }
  determination on_save_customer on save { field PartyID; }
  determination on_modify_customer on modify { field PartyID; }

  // Associations:
  association _Available {  create ( features : instance, authorization : update ); with draft; }
  association _Outbound {  create ( features : instance, authorization : update ); with draft; }

  field ( readonly, numbering : managed ) ShipmentUUID;
  field ( readonly ) ShipmentID;

  side effects
  {
    action release affects field Released;
    field PartyID affects $self;
    action create_tariff_document affects entity _Outbound;
  }

}


define behavior for ZI_AVAILABLE_003 alias Available
persistent table zavailable003
draft table zavailable003d
lock dependent by _Shipment
authorization dependent by _Shipment
etag master LocalLastChangedAt
{

  update;
  delete;

  // Events:
  determination on_available_create on modify { field AvailableUUID; }
  determination on_outbound_delivery_modify on modify { field OutboundDelivery; }

  // Associations:
  association _Shipment { with draft; }

  field ( readonly, numbering : managed ) AvailableUUID;
  field ( readonly ) ShipmentUUID;
  field ( readonly ) AvailableID;

}


define behavior for ZI_OUTBOUND_003 alias Outbound
persistent table zoutbound003
draft table zoutbound003d
lock dependent by _Shipment
authorization dependent by _Shipment
etag master LocalLastChangedAt
{

  update;
  delete;

  // Events:
  determination on_outbound_create on modify { field OutboundUUID; }

  // Associations:
  association _Shipment { with draft; }

  field ( readonly, numbering : managed ) OutboundUUID;
  field ( readonly ) ShipmentUUID;
  field ( readonly ) OutboundID;

}